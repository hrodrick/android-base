apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-kapt'

apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion 28
    dataBinding.enabled = true
    defaultConfig {
        applicationId "com.rootstrap.android"
        minSdkVersion 15
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }


    buildTypes {
        debug {
        }

        release {
        }

        applicationVariants.all { variant ->
        }
    }

    configurations {
        ktlint
    }

    flavorDimensions "api"

    productFlavors {

        dev {
            dimension "api"
            applicationIdSuffix ".dev"
            versionNameSuffix "-dev"
            buildConfigField("String", "API_URL", "\"https://dev.com/api/\"")
        }

        staging {
            dimension "api"
            applicationIdSuffix ".staging"
            versionNameSuffix "-staging"
            buildConfigField("String", "API_URL", "\"https://staging.com/api/\"")
        }

        prod {
            dimension "api"
            applicationIdSuffix ".prod"
            versionNameSuffix "-prod"
            buildConfigField("String", "API_URL", "\"https://prod.com/api/\"")
        }
    }

    applicationVariants.all {
        assembleProvider.get().dependsOn.add(tasks["lint${name.capitalize()}"])
        outputs.all { output ->
            def apk = output.outputFile
            def newName = apk.name.replace(".apk", "-v" + versionName + ".apk")
            newName = newName.replace("-" + buildType.name, "")

            outputFileName = new File("./apks/" + newName)
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        lintConfig file('lint.xml')
        // If set to true, turns off analysis progress reporting by lint.
        quiet true
        // if set to true (default), stops the build if errors are found.
        abortOnError true
        // if true, only report errors.
        ignoreWarnings false
    }

    task ktlint(type: JavaExec, group: "verification") {
        description = "Check Kotlin code style."
        classpath = configurations.ktlint
        main = "com.pinterest.ktlint.Main"
        args "src/**/*.kt"
        // to generate report in checkstyle format prepend following args:
        // "--reporter=plain", "--reporter=checkstyle,output=${buildDir}/ktlint.xml"
        // see https://github.com/pinterest/ktlint#usage for more
    }

    check.dependsOn ktlint

    task ktlintFormat(type: JavaExec, group: "formatting") {
        description = "Fix Kotlin code style deviations."
        classpath = configurations.ktlint
        main = "com.pinterest.ktlint.Main"
        args "-F", "src/**/*.kt"
    }

    manifest {
        attributes("Lint-Registry-v2": "com.rootstrap.android.lint.LintRegistry")
    }

}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    //---- ANDROID SUPPORT
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support:support-compat:28.0.0'
    implementation 'com.android.support:exifinterface:28.0.0'
    implementation 'com.android.support:cardview-v7:28.0.0'
    implementation 'com.android.support:design:28.0.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    //---- TEST
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    testImplementation 'junit:junit:4.12'
    //---ANDROID ARCH ROOM
    implementation 'android.arch.persistence.room:runtime:1.1.1'
    kapt "android.arch.persistence.room:compiler:1.1.1"
    //---ANDROID ARCH LIFECYCLE
    implementation 'android.arch.lifecycle:common-java8:1.1.1'
    kapt "android.arch.lifecycle:compiler:1.1.1"
    implementation 'android.arch.lifecycle:runtime:1.1.1'
    implementation 'android.arch.lifecycle:extensions:1.1.1'
    //---RETROFIT HTTP REST API REQUESTS
    implementation 'com.squareup.retrofit2:retrofit:2.4.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.4.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.10.0'
    implementation 'com.squareup.okhttp3:okhttp:3.10.0'
    //---GOOGLE JSON SERIALIZER/DESERIALIZER
    implementation 'com.google.code.gson:gson:2.8.5'
    //---BETTER TIME HANDLING
    implementation 'net.danlew:android.joda:2.9.9.1'
    //---IMAGES
    implementation 'com.squareup.picasso:picasso:2.71828'

    kapt 'com.android.tools.external.com-intellij:intellij-core:26.4.1'
    annotationProcessor 'com.android.tools.external.com-intellij:intellij-core:26.4.1'

    testImplementation "com.android.tools.lint:lint-api:26.4.1"
    testImplementation "com.android.tools.lint:lint-checks:26.4.1"

    testImplementation "com.android.tools.lint:lint:26.4.1"
    testImplementation "com.android.tools.lint:lint-tests:26.4.1"
    testImplementation "com.android.tools:testutils:26.4.1"

    ktlint "com.pinterest:ktlint:0.33.0"
}
